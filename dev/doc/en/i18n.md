Hi,

Welcome to i18n showcase. In following text we'll 
present how to add translations and how 
to use-translate inside of UI components.

Before that, lets go through how toddler 
approaches translation. In namespace **toddler.i18n**
there is definition for Translator protocol. 
It is simple definition that "translates" 
based on some locale and optional options.

```clojure
(defprotocol Translator
  (translate
    [this]
    [this locale]
    [this locale options]
    "Translates input data by using additional opitons"))
```

Implementations of this protocol exist for:

 * **keyword**
 * **number**
 * **Date**
 * **UUID**

For each impementation namespace exists that is 
responsible for managing where to store translations and how to translate given type.


#### Examples


Formatting numbers works in two ways. First if keyword option 
is passed that specifies locale, than number is formated to 
that locale. When string is passed as second option, 
translate will try to format number as currency by 
looking for specified currency code (USD, EUR)


```clojure
(translate 100000.001 :en_US) ;; "100,000.001"
(translate 100000.001 :fr)    ;; "100 000,001"
(translate 100000.001 "JPY")  ;; "¥100,000.00"
(translate 100000.001 "INR")  ;; "₹100,000.00"
(translate 100000.001 "BRL")  ;; "R$ 100,000.00"

;; Default format is short-datetime
(translate (js/Date.) :en_US)                   ;; "1/10/25, 1:43 PM"
(translate (js/Date.) :en_US :date)             ;; "1/10/25"
(translate (js/Date.) :en_US :full-datetime)    ;; "Friday, January 10, 2025 at 1:44:27 PM UTC+1"
(translate (js/Date.) :en_US :medium-datetime)  ;; "Jan 10, 2025, 1:45:06 PM"
```

Following formatting options are available for date type:

 * **:full-date**
 * **:long-date**
 * **:medium-date**
 * **:date**
 * **:full-time**
 * **:long-time**
 * **:medium-time**
 * **:time**
 * **:full-datetime**
 * **:long-datetime**
 * **:medium-datetime**
 * **:datetime**
 * **:calendar**

## ADDING TRANSLATIONS

In previous sections we were translating numbers 
and dates. In most cases this is fine for displaying 
values, but what about part of application that 
is static and provides context of application state.

For this situations it would be preferable 
to register keywords or UUIDs and add 
translations so that toddler.i18n/translate can work as expected.

This is how you add translations. Following is 
actual namespace from toddler library that 
adds-translations that are common enough.


```clojure
(ns toddler.i18n.common
  (:require
   [toddler.i18n.keyword :refer [add-translations]]))

(add-translations
 (merge
  #:open {:default "Open" :hr "Otvori" :es "Abrir" :de "Öffnen" :fr "Ouvrir"}
  #:neutral {:default "Neutral" :hr "Neutralan" :es "Neutral" :de "Neutral" :fr "Neutre"}
  #:positive {:default "Positive" :hr "Pozitivan" :es "Positivo" :de "Positiv" :fr "Positif"}
  #:negative {:default "Negative" :hr "Negativan" :es "Negativo" :de "Negativ" :fr "Négatif"}
  #:warn {:default "Warning" :hr "Upozorenje" :es "Advertencia" :de "Warnung" :fr "Avertissement"}
  #:ok {:default "OK" :hr "OK" :es "OK" :de "OK" :fr "OK"}
  #:cancel {:default "Cancel" :hr "Odbaci" :es "Cancelar" :de "Abbrechen" :fr "Annuler"}
  #:add {:default "Add" :hr "Dodaj" :es "Añadir" :de "Hinzufügen" :fr "Ajouter"}
  #:delete {:default "Delete" :hr "Izbriši" :es "Eliminar" :de "Löschen" :fr "Supprimer"}
  #:remove {:default "Remove" :hr "Ukloni" :es "Eliminar" :de "Entfernen" :fr "Supprimer"}
  #:first-name {:default "First Name" :hr "Ime" :es "Nombre" :de "Vorname" :fr "Prénom"}
  #:last-name {:default "Last Name" :hr "Prezime" :es "Apellido" :de "Nachname" :fr "Nom"}
  #:address {:default "Address" :hr "Adresa" :es "Dirección" :de "Adresse" :fr "Adresse"}
  #:post-code {:default "Post Code" :hr "Poštanski kod" :es "Código Postal" :de "Postleitzahl" :fr "Code Postal"}
  #:credit-card {:default "Credit Card" :hr "Kreditna kartica" :es "Tarjeta de Crédito" :de "Kreditkarte" :fr "Carte de Crédit"}
  #:image {:default "Image" :hr "Slika" :es "Imagen" :de "Bild" :fr "Image"}
  #:example {:default "Example" :hr "Primjer" :es "Ejemplo" :de "Beispiel" :fr "Exemple"}
  #:examples {:default "Examples" :hr "Primjeri" :es "Ejemplos" :de "Beispiele" :fr "Exemples"}
  #:locale {:default "English"
            :hr "Hrvatski"        ;; Croatian
            :de "Deutsch"         ;; German
            :fr "Français"        ;; French
            :es "Español"         ;; Spanish
            :it "Italiano"        ;; Italian
            :pt "Português"       ;; Portuguese
            :ru "Русский"         ;; Russian
            :zh "中文"            ;; Chinese
            :ja "日本語"          ;; Japanese
            :ko "한국어"          ;; Korean
            :ar "العربية"         ;; Arabic
            :nl "Nederlands"      ;; Dutch
            :sv "Svenska"         ;; Swedish
            :fi "Suomi"           ;; Finnish
            :no "Norsk"           ;; Norwegian
            :da "Dansk"           ;; Danish
            :pl "Polski"          ;; Polish
            :tr "Türkçe"          ;; Turkish
            :el "Ελληνικά"        ;; Greek
            :cs "Čeština"         ;; Czech
            :sk "Slovenčina"      ;; Slovak
            :hu "Magyar"          ;; Hungarian
            :ro "Română"          ;; Romanian
            :bg "Български"       ;; Bulgarian
            :sr "Српски"          ;; Serbian
            :uk "Українська"      ;; Ukrainian
            :he "עברית"           ;; Hebrew
            :th "ไทย"             ;; Thai
            :vi "Tiếng Việt"      ;; Vietnamese
            :sw "Kiswahili"       ;; Swahili
            }))
```



### ADDING LOCALE
Sometimes it is awkward to add translations in this fashion.
What if you wan't to separate translations based on
locale and keep in different files. 
So that it can be loaded on demand.

Sure that is possible. Check it out:
```clojure
(add-locale
 #:default {:light "Light"
            :fatal.error "Shit hit the fan!"
            :some.specific.thing "Gold"})

      
 (translate :some.specific.thing :en) ;; "Gold"
 (translate :fatal.error :hr) ;; "Shit hit the fan!"
```


### TRANSLATING COMPONENTS
Sooner or later even this won't be enough to use-translate. 
Sometimes situation will get complex, using many keywords
to translate sentence parts or text will be hard.

In cases like this it is possible to create Helix component
and add that component to translations. Then render that 
component in respect to selected locale.


```clojure
(defnc i18n-example []
  (let [locale (toddler.core/use-current-locale)]
    (d/div
     {:className (css :p-4 :mt-4 :font-semibold :bg-yellow-200 :rounded-xl :text-black)}
     (d/div
      {:className "content"}
      (case locale
        :de
        (d/p "Hallo, dies ist ein Beispielkomponent. "
             "Versuchen Sie, die Sprache zu ändern, um zu sehen, wie sich dieser Satz ändert.")
        :fr
        (d/p "Bonjour, ceci est un composant d'exemple. "
             "Essayez de changer la langue pour voir comment cette phrase change.")
        :es
        (d/p "Hola, este es un componente de ejemplo. "
             "Intenta cambiar la configuración regional para ver cómo cambia esta oración.")
        :hr
        (d/p "Pozdrav, ovo je primjer komponente. "
             "Pokušajte promijeniti lokalne postavke da vidite kako će se ova rečenica promijeniti.")
        (d/p "hello this is example component. "
             "Try and change locale to see how this sentence will change"))))))
```


<div id="component-translation-example"></div>


# Hej, ide novi text
